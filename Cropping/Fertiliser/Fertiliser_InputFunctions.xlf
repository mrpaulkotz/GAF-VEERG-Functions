Fertiliser_FertiliserProductLookup
=LAMBDA(Product, ProductAttribute, ProductArray, ProductArrayColumn, ProductArrayHeaders,
  IFERROR(
    INDEX(
      ProductArray,
      XMATCH(TRIM(Product), ProductArrayColumn, 0),
      XMATCH(TRIM(ProductAttribute), ProductArrayHeaders, 0)
    ),
    ""
  )
);


Fertiliser_SumProductApplied
=LAMBDA(
  LookupTarget1,LookupArray1,AmountApplied,[LookupTarget2],[LookupArray2],
  LET(
    criteria1,--(LookupArray1=LookupTarget1),
    criteria2,IF(ISOMITTED(LookupTarget2),1,--(LookupArray2=LookupTarget2)),
    SUMPRODUCT(criteria1*criteria2*AmountApplied)
  )
);

Fertiliser_TotalSoilAmendmentApplied
=LAMBDA(
  AmountApplied, AmendmentPercent,
  SUMPRODUCT(AmountApplied * AmendmentPercent)
);



Fertiliser_TotalChemicalCompoundAppliedAllIngredients
=LAMBDA(
  ChemicalCompound,
  AmountApplied,
  ProductIngredient1,
  ProductIngredientPercent1,
  [ProductIngredient2],
  [ProductIngredientPercent2],
  [ProductIngredient3],
  [ProductIngredientPercent3],
  [ProductIngredient4],
  [ProductIngredientPercent4],
  [ProductIngredient5],
  [ProductIngredientPercent5],
  [ProductIngredient6],
  [ProductIngredientPercent6],
  LET(
    calculatedAmount1, (AmountApplied*ProductIngredientPercent1),
    calculatedAmount2, (AmountApplied*ProductIngredientPercent2),
    calculatedAmount3, (AmountApplied*ProductIngredientPercent3), 
    calculatedAmount4, (AmountApplied*ProductIngredientPercent4), 
    calculatedAmount5, (AmountApplied*ProductIngredientPercent5), 
    calculatedAmount6, (AmountApplied*ProductIngredientPercent6),  
    Fertiliser_SumProductApplied(
      ChemicalCompound, ProductIngredient1, calculatedAmount1
    )+
    IFERROR(Fertiliser_SumProductApplied(
      ChemicalCompound, ProductIngredient2, calculatedAmount2
    ), 0)+
    IFERROR(Fertiliser_SumProductApplied(
      ChemicalCompound, ProductIngredient3, calculatedAmount3
    ), 0)+
    IFERROR(Fertiliser_SumProductApplied(
      ChemicalCompound, ProductIngredient4, calculatedAmount4
    ), 0)+
    IFERROR(Fertiliser_SumProductApplied(
      ChemicalCompound, ProductIngredient5, calculatedAmount5
    ), 0)+
    IFERROR(Fertiliser_SumProductApplied(
      ChemicalCompound, ProductIngredient6, calculatedAmount6
    ), 0)
  )
);

Fertiliser_TotalChemicalCompoundtAppliedBySourceAllIngredients
=LAMBDA(
  ChemicalCompound,
  Source,
  AmountApplied,
  ProductIngredient1,
  ProductIngredientPercent1,
  ProductIngredient1Source,
  [ProductIngredient2],
  [ProductIngredientPercent2],
  [ProductIngredient2Source],
  [ProductIngredient3],
  [ProductIngredientPercent3],
  [ProductIngredient3Source],
  [ProductIngredient4],
  [ProductIngredientPercent4],
  [ProductIngredient4Source],
  [ProductIngredient5],
  [ProductIngredientPercent5],
  [ProductIngredient5Source],
  [ProductIngredient6],
  [ProductIngredientPercent6],
  [ProductIngredient6Source],
  LET(
    calculatedAmount1, (AmountApplied*ProductIngredientPercent1),
    calculatedAmount2, (AmountApplied*ProductIngredientPercent2),
    calculatedAmount3, (AmountApplied*ProductIngredientPercent3), 
    calculatedAmount4, (AmountApplied*ProductIngredientPercent4), 
    calculatedAmount5, (AmountApplied*ProductIngredientPercent5), 
    calculatedAmount6, (AmountApplied*ProductIngredientPercent6),  
    Fertiliser_SumProductApplied(
      ChemicalCompound, ProductIngredient1, calculatedAmount1, Source, ProductIngredient1Source
    )+
    IFERROR(Fertiliser_SumProductApplied(
      ChemicalCompound, ProductIngredient2, calculatedAmount2, Source, ProductIngredient2Source
    ), 0)+
    IFERROR(Fertiliser_SumProductApplied(
      ChemicalCompound, ProductIngredient3, calculatedAmount3, Source, ProductIngredient3Source
    ), 0)+
    IFERROR(Fertiliser_SumProductApplied(
      ChemicalCompound, ProductIngredient4, calculatedAmount4, Source, ProductIngredient4Source
    ), 0)+
    IFERROR(Fertiliser_SumProductApplied(
      ChemicalCompound, ProductIngredient5, calculatedAmount5, Source, ProductIngredient5Source
    ), 0)+
    IFERROR(Fertiliser_SumProductApplied(
      ChemicalCompound, ProductIngredient6, calculatedAmount6, Source, ProductIngredient6Source
    ), 0)
  )
);

Fertiliser_GetScope3EF
=LAMBDA(
    ChemicalCompound, Scope3Array, ChemicalCompoundColumn, Source, SourceRow,
    INDEX(Scope3Array,
       MATCH(ChemicalCompound, ChemicalCompoundColumn, 0),
       MATCH(Source, SourceRow, 0)
    )
);

Fertiliser_GetChemicalCompoundElementFraction
=LAMBDA(
    ChemicalCompound, Element, ChemicalCompoundArray, ChemicalCompoundColumn, ElementRow,
    INDEX(ChemicalCompoundArray,
      MATCH(ChemicalCompound, ChemicalCompoundColumn, 0),
      MATCH(Element, ElementRow, 0)
    )
);