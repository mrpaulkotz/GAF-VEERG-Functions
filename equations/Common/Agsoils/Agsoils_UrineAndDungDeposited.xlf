/*
============================================================================
National Inventory Report 2023 reference:
Urine and dung deposited by grazing animals (3.D.a.3)
============================================================================
*/


/* 
--------------------------------------
UrineAndDungDeposited
Nitrous oxide production from urine and dung deposited by grazing animals on pasture,
range and paddock
EN2Odir
t N2O
EN2Odir = ((AFjm=14 * EFPRP * CgN2O) + (AUjm=14 * EFPRP * CgN2O)) * 10^-3
AFjm : mass of nitrogen in dung deposited on pasture, range and paddock : kg N
AUjm : mass of nitrogen in urine deposited on pasture, range and paddock : kg N
EFPRP : direct N2O emission factor for pasture, range and paddock : kg N2O-N/kg N
CgN2O : factor to convert elemental mass of N2O to molecular mass
--------------------------------------
*/

AgSoils_UrineAndDungDeposited_GWP
 =LAMBDA(
    EN2Odir,GWPN2O,
     (EN2Odir * GWPN2O)
   );

AgSoils_UrineAndDungDeposited
  =LAMBDA(
    AFjm, AUjm, EFPRP, CgN2O,
    ((AFjm * EFPRP * CgN2O) + (AUjm * EFPRP * CgN2O)) * 10^-3
  )
;

AgSoils_UrineAndDungDeposited_Title
  =LAMBDA("Nitrous oxide production from urine and dung deposited by grazing animals on pasture, range and paddock");

AgSoils_UrineAndDungDeposited_Variable
  =LAMBDA("EN2Odir");

AgSoils_UrineAndDungDeposited_Unit
  =LAMBDA("t N2O");

AgSoils_UrineAndDungDeposited_NIRReference
  =LAMBDA("National Inventory Report Volume 1, 2023: 3.D.a.3");

AgSoils_UrineAndDungDeposited_ReadableEquation
  =LAMBDA("EN2Odir = ((AFjm=14 * EFPRP * CgN2O) + (AUjm=14 * EFPRP * CgN2O)) * 10^-3");

AgSoils_UrineAndDungDeposited_Arguments
  =LAMBDA(
    MAKEARRAY(4, 3, 
      LAMBDA(r,c, 
        INDEX({
          "AFjm","mass of N in dung deposited on pasture, range and paddock","kg N";
          "AUjm","mass of N in urine deposited on pasture, range and paddock","kg N";
          "EFPRP","direct N2O emission factor for pasture, range and paddock","kg N2O-N/kg N";
          "CgN2O","factor to convert elemental mass of N2O to molecular mass",""
        }, r, c)
      )
    )
  );



