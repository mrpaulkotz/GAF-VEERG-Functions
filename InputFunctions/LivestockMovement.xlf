LivestockMovement_HeadLostDuringMonth
= LAMBDA(Month, LivestockClass,
    IFERROR(
    SUM(
      FILTER(
        Input_HerdMovement[Head],
        (Input_HerdMovement[Date] >= EOMONTH(Month,-1)+1) *
        (Input_HerdMovement[Date] <= EOMONTH(Month,0)) *
        (Input_HerdMovement[From] = LivestockClass)
      )
    ),
    0
  )
);

LivestockMovement_HeadAddedDuringMonth
= LAMBDA(Month, LivestockClass,
    IFERROR(
    SUM(
      FILTER(
        Input_HerdMovement[Head],
        (Input_HerdMovement[Date] >= EOMONTH(Month,-1)+1) *
        (Input_HerdMovement[Date] <= EOMONTH(Month,0)) *
        (Input_HerdMovement[To] = LivestockClass)
      )
    ),
    0
  )
);

LivestockMovement_HeadRemainingAtMonthEnd
=LAMBDA(
    Month, LivestockClass,HeadAtStartOfMonth,
    MAX(
        HeadAtStartOfMonth-LivestockMovement_HeadLostDuringMonth(Month,LivestockClass), 
        0
    )
);


LivestockMovement_GetMovementLiveweightGain
=LAMBDA(LivestockClass, MovementDate,
  LET(
    ym, TEXT(MovementDate,"yyyymm"),
    IFERROR(
      INDEX(
        FILTER(
          Herd[Liveweight gain in kg/head/day],
          (TEXT(Herd[Month],"yyyymm") = ym) *
          (Herd[Livestock class] = LivestockClass)
        ),
        1
      ),
      ""
    )
  )
);

LivestockMovement_DaysMovementToMonthEnd
=LAMBDA(
  MovementDate,
  DATEDIF(MovementDate, EOMONTH(MovementDate, 0), "d")
);

LivestockMovement_LiveweightGrownDuringMonthPerHead
=LAMBDA(LivestockClass, MovementDate,
  IFERROR( 
    LivestockMovement_GetMovementLiveweightGain(LivestockClass, MovementDate)*
    LivestockMovement_DaysMovementToMonthEnd(MovementDate)
  , 0)
);

LivestockMovement_LiveweightAddedDuringMonth
= LAMBDA(LivestockClass, MovementDate, WeightAtMovementDate, Head,
IFERROR(
    Head*
        (
        WeightAtMovementDate+
        LivestockMovement_LiveweightGrownDuringMonthPerHead(LivestockClass, MovementDate)
        )
, 0)
);

LivestockMovement_LiveweightGrownFromMovementDateTotal
=LAMBDA(
  LivestockClass, MovementDate, Head,
  LivestockMovement_LiveweightGrownDuringMonthPerHead(LivestockClass, MovementDate) *
  Head
);

LivestockMovement_LiveweightGrownFromMonthStartPerHead
=LAMBDA(LivestockClass, Month,
  LivestockMovement_LiveweightGrownDuringMonthPerHead(LivestockClass, EOMONTH(Month,-1)+1)
);

LivestockMovement_LiveweightGrownFromMonthStartTotal
=LAMBDA(LivestockClass, Month, HeadAtStartOfMonth,
  LivestockMovement_HeadRemainingAtMonthEnd(Month, LivestockClass, HeadAtStartOfMonth) *
  LivestockMovement_LiveweightGrownFromMonthStartPerHead(LivestockClass, Month)
);

LivestockMovement_HeadAtEndOfMonth
= LAMBDA(HeadAtStart, Month, LivestockClass,
MAX(
    HeadAtStart
    + 
    LivestockMovement_HeadAddedDuringMonth(Month, LivestockClass)
    - 
    LivestockMovement_HeadLostDuringMonth(Month, LivestockClass)
    ,0)
);

LivestockMovement_LiveweightFromStartOfMonth
= LAMBDA(LiveweightAtStart, LiveweightGainPerDay, Month,
  LiveweightAtStart+
  (LiveweightGainPerDay*DATEDIF(EDATE(Month, 0), EDATE(Month, 1), "d"))
);

LivestockMovement_TotalLiveweightAddedDuringMonth
= LAMBDA(
    Month, LivestockClass,
    IFERROR(
    SUM(
      FILTER(
        Input_HerdMovement[Total weight gained since joining],
        (Input_HerdMovement[Date] >= EOMONTH(Month,-1)+1) *
        (Input_HerdMovement[Date] <= EOMONTH(Month,0)) *
        (Input_HerdMovement[To] = LivestockClass)
      )
    ),
    0
  )
);

LivestockMovement_TotalLiveweightGrownDuringMonth
= LAMBDA(
    Month, LivestockClass,
    IFERROR(
    SUM(
      FILTER(
        Input_HerdMovement[Liveweight grown since joining],
        (Input_HerdMovement[Date] >= EOMONTH(Month,-1)+1) *
        (Input_HerdMovement[Date] <= EOMONTH(Month,0)) *
        (Input_HerdMovement[To] = LivestockClass)
      )
    ),
    0
  )
);

LivestockMovement_AverageLiveweightAddedDuringMonth
=LAMBDA(
    Month, LivestockClass,
    IFERROR(
      LivestockMovement_TotalLiveweightAddedDuringMonth(Month, LivestockClass)/
      LivestockMovement_HeadAddedDuringMonth(Month, LivestockClass)
      , 0
    )
);

LivestockMovement_AverageLiveweightRemainingHead
=LAMBDA(
  Month, LiveweightAtStart, LiveweightGain,
  LiveweightAtStart+(LiveweightGain*DATEDIF(EDATE(Month, 0), EDATE(Month, 1), "d"))
);

LivestockMovement_ProportionHeadRemainingAtEndOfMonth
=LAMBDA(
    Month, LivestockClass, HeadAtStart,
    IFERROR(
      MIN(1,
        LivestockMovement_HeadRemainingAtMonthEnd(Month, LivestockClass, HeadAtStart)/ 
        LivestockMovement_HeadAtEndOfMonth(HeadAtStart, Month, LivestockClass)
      ), 0
    )
);

LivestockMovement_ProportionHeadAddedDuringMonth
=LAMBDA(
    Month, LivestockClass, HeadAtStart,
    IFERROR(
      MIN(1,
        LivestockMovement_HeadAddedDuringMonth(Month, LivestockClass)/ 
        LivestockMovement_HeadAtEndOfMonth(HeadAtStart, Month, LivestockClass)
      ), 0
    )
);

LivestockMovement_ProportionalAverageLiveweightAddedDuringMonth
=LAMBDA(
    Month, LivestockClass, HeadAtStart,
    LivestockMovement_ProportionHeadAddedDuringMonth(Month, LivestockClass, HeadAtStart)*
    LivestockMovement_AverageLiveweightAddedDuringMonth(Month, LivestockClass)
);

LivestockMovement_ProportionalAverageLiveweightRemainingAtEndOfMonth
=LAMBDA(
    Month, LivestockClass, HeadAtStart, LiveweightAtStart, LiveweightGain,
    LivestockMovement_ProportionHeadRemainingAtEndOfMonth(Month, LivestockClass, HeadAtStart)*
    LivestockMovement_AverageLiveweightRemainingHead(Month, LiveweightAtStart, LiveweightGain)
);

LivestockMovement_GetLiveweightAtStart
=LAMBDA(
  LivestockClass,
  HLOOKUP(LivestockClass, Range_LivestockStartingValues, 3, FALSE)
);

LivestockMovement_GetLiveweightAtEnd
=LAMBDA(
  LivestockClass,
  HLOOKUP(LivestockClass, Range_LivestockStartingValues, 4, FALSE)
);

LivestockMovement_GetGrowthFactor
=LAMBDA(
  Month, LivestockClass,  
  XLOOKUP(
    Month,
   CrudeProteinGrowthFactor[Month],
  INDEX(
    CrudeProteinGrowthFactor, , 
    MATCH(LivestockClass, CrudeProteinGrowthFactor[#Headers], 0)
  )
));

LivestockMovement_GetDurationOfStay
=LAMBDA(
  LivestockClass,
  HLOOKUP(LivestockClass, Table_DurationOfStay[#All], 2, FALSE)
);

LivestockMovement_CalculateLiveweightGain
=LAMBDA(
  Month, LivestockClass,
  (
    (LivestockMovement_GetLiveweightAtEnd(LivestockClass) - 
    LivestockMovement_GetLiveweightAtStart(LivestockClass))
    / LivestockMovement_GetDurationOfStay(LivestockClass)
  )* LivestockMovement_GetGrowthFactor(Month, LivestockClass)
);

AverageLiveweightEndOfMonth
=LAMBDA(
  Month, LivestockClass, HeadAtStart, LiveweightAtStart, LiveweightGain,
  LivestockMovement_ProportionalAverageLiveweightAddedDuringMonth(Month, LivestockClass, HeadAtStart)+
  LivestockMovement_ProportionalAverageLiveweightRemainingAtEndOfMonth(Month, LivestockClass, HeadAtStart, LiveweightAtStart, LiveweightGain)
);

AverageLiveweightMonth
=LAMBDA(
  LiveweightAtMonthStart, LiveweightAtMonthEnd,
  IFS(
    LiveweightAtMonthStart <=0, LiveweightAtMonthEnd,
    LiveweightAtMonthEnd <=0, LiveweightAtMonthStart,
    TRUE, (LiveweightAtMonthStart+LiveweightAtMonthEnd)/2
  )
);

LivestockMovement_TotalLiveweightGrownDuringMonthAddedAndRemaining
=LAMBDA(
  Month, LivestockClass, HeadAtStart,
  LivestockMovement_LiveweightGrownFromMonthStartTotal(LivestockClass, Month, HeadAtStart)+
  LivestockMovement_TotalLiveweightGrownDuringMonth(Month, LivestockClass)
);

LivestockMovement_CalculateStayDurationToEnd
=LAMBDA(
  LivestockClass, ReportingPeriodEndDate,
  DATEDIF(
    MINIFS(Input_HerdMovement[Date], Input_HerdMovement[To],LivestockClass), ReportingPeriodEndDate, "d"
  )
);

LivestockMovement_AverageHeadMonth
=LAMBDA(
  HeadAtMonthStart, HeadAtMonthEnd,
  IFS(
    HeadAtMonthStart <=0, HeadAtMonthEnd,
    HeadAtMonthEnd <=0, HeadAtMonthStart,
    TRUE, (HeadAtMonthStart+HeadAtMonthEnd)/2
  )
);

LivestockMovement_TotalBirthsDuringMonth
= LAMBDA(
    Month,
    IFERROR(
    SUM(
      FILTER(
        Input_HerdMovement[Head],
        (Input_HerdMovement[Date] >= EOMONTH(Month,-1)+1) *
        (Input_HerdMovement[Date] <= EOMONTH(Month,0)) *
        (Input_HerdMovement[Movement type (select)] = "Birth")
      )
    ),
    0
  )
);

LivestockMovement_CalculateProportionCowsCalving
= LAMBDA(
  Births, Head,
  IFERROR(
    Births / Head,
    0
  )
);

LivestockMovement_MovementLiveweightSold
=LAMBDA(
  MovementType, Head, Liveweight,
  IF(MovementType = "Sale", Head*Liveweight, "")
);