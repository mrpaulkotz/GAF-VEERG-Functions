Utility_GetArrayTableRowStartNumber
=LAMBDA(
    TableheaderCell,RowsBetweenHeaderandArray,
    ROW(TableheaderCell) + RowsBetweenHeaderandArray - 1
);

Utility_GetArrayTableColumnStartNumber
=LAMBDA(
    TableheaderCell,
    COLUMN(TableheaderCell)-1
);

Utility_DisplayArrayInTable
=LAMBDA(
 ArrayData, TableheaderCell, RowsBetweenHeaderandArray,
 IFERROR(
     INDEX(
            UNIQUE(ArrayData), 
            ROW()-Utility_GetArrayTableRowStartNumber(TableheaderCell, RowsBetweenHeaderandArray), 
            COLUMN()-Utility_GetArrayTableColumnStartNumber(TableheaderCell)
        ), ""
    )
);

Utility_LookupValueInTable
=LAMBDA(
    LookupValue, LookupArray, ReturnArray,
    XLOOKUP(LookupValue, LookupArray, ReturnArray, "")
);

Utility_LookupValueInTableNumber
=LAMBDA(
    LookupValue, LookupArray, ReturnArray,
    IF(ISNUMBER(
        XLOOKUP(LookupValue, LookupArray, ReturnArray, "")
    ), XLOOKUP(LookupValue, LookupArray, ReturnArray, ""), 0)
);

Utility_SplitStringIntoArray
=LAMBDA(
    Cell, Delimiter, [CharacterToRemove1], [CharacterToRemove2],
        FILTERXML(
        "<t><s>" &
        SUBSTITUTE(
            SUBSTITUTE(SUBSTITUTE(Cell,CharacterToRemove1,""),CharacterToRemove2,""),
            Delimiter,"</s><s>"
        ) &
        "</s></t>",
        "//s"
    )
);

Utility_GetClimateZone
=LAMBDA(
    avgTemp,avgRain,frostDays,elev,map_pet,
    LET(
        mask,
        (VALUE(Table_SourceData_ClimateZones[Min temp])<=avgTemp)*
        (VALUE(Table_SourceData_ClimateZones[Max temp])>=avgTemp)*
        (VALUE(Table_SourceData_ClimateZones[Min rainfall])<=avgRain)*
        (VALUE(Table_SourceData_ClimateZones[Max rainfall])>=avgRain)*
        (VALUE(Table_SourceData_ClimateZones[Min frost days])<=frostDays)*
        (VALUE(Table_SourceData_ClimateZones[Max frost days])>=frostDays)*
        (VALUE(Table_SourceData_ClimateZones[Min elevation])<=elev)*
        (VALUE(Table_SourceData_ClimateZones[Max elevation])>=elev)*
        (VALUE(Table_SourceData_ClimateZones[Min MAP:PET])<=map_pet)*
        (VALUE(Table_SourceData_ClimateZones[Max MAP:PET])>=map_pet),
        FILTER(Table_SourceData_ClimateZones[Climate zone],mask, "No match")
    )
);

Utility_LookupItemFromMinMax
=LAMBDA(
    LookupValue, MinArray, MaxArray, ItemArray,
    LET(
        mask,
        (VALUE(MinArray)<=LookupValue)*
        (VALUE(MaxArray)>=LookupValue),
        FILTER(ItemArray, mask, "No match")
    )
);